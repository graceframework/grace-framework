apply plugin:'application'

mainClassName = "org.grails.cli.GrailsCli"

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

ext {
    gradleToolingApiVersion = gradle.gradleVersion
}

configurations {
	dependenciesBom
}

dependencies {
    api project(":grails-api")
    api project(":grails-bootstrap")
    api project(":grails-cli")
    api project(":grails-gradle-model")
    api project(":grails-util")

    api libs.ant
    api libs.gradle.tooling.api
    api libs.groovy.ant
    api libs.groovy.json
    api libs.groovy.jmx
    api libs.jansi
    api libs.jline
    api libs.spring.boot.cli, {
        exclude group: "org.codehaus.groovy", module: "groovy"
    }
    dependenciesBom project(path: ":grails-bom", configuration: 'effectiveBom')
    implementation(libs.maven.resolver.provider) {
        exclude group: "com.google.guava", module: "guava"
    }
    implementation(libs.maven.resolver.connectorBasic)
    implementation(libs.maven.resolver.impl)
    implementation(libs.maven.resolver.transportFile)
    implementation(libs.maven.resolver.transportHttp) {
        exclude group: "org.slf4j", module:"jcl-over-slf4j"
    }
    runtimeOnly libs.slf4j.simple
}

task syncGrailsDependenciesBom(type: Sync) {
	destinationDir = file("${buildDir}/generated-resources/org/grails/cli/boot/dependencies")
	from configurations.dependenciesBom
}

sourceSets {
	main {
		output.dir("${buildDir}/generated-resources", builtBy: "syncGrailsDependenciesBom")
	}
}

eclipse {
    classpath {
       file {
           whenMerged { classpath ->
                classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "src/test/resources" }?.excludes=["profiles-repository/**", "gradle-sample/**"]
           }
       }
    }
}

apply from: "../gradle/integration-test.gradle"

integrationTest {
    // jline doesn't use jline.terminal system property when TERM is dumb so use different TERM value for tests
    // https://github.com/jline/jline2/blob/6a1b6bf/src/main/java/jline/TerminalFactory.java#L54-L57
    environment 'TERM', 'xterm'
    // execute in single thread
    maxParallelForks = 1
}
