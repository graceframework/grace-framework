buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
        classpath "com.bmuschko:gradle-nexus-plugin:2.3.1"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

apply plugin: 'java-gradle-plugin'

configurations {
    documentation
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-xml'
}

dependencies {
    documentation libs.fusesource.jansi
    documentation libs.groovy.picocli

    api gradleApi()
    api "io.github.gradle-nexus:publish-plugin:1.1.0"
    api "com.bmuschko:gradle-nexus-plugin:2.3.1"
    api project(":grails-bootstrap")
    api project(":grails-gradle-model")
    api project(":grails-shell"), {
        exclude group: "org.slf4j", module: "slf4j-simple"
    }

    api libs.maven.model
    api libs.spring.boot.gradle.plugin
    api libs.dependency.management.plugin
    api "com.netflix.nebula:gradle-extra-configurations-plugin:7.0.0"
}

gradlePlugin {
    plugins {
        grailsCore {
            displayName = "Grails Core Gradle Plugin"
            description = 'The main Grails gradle plugin implementation'
            id = 'org.grails.grails-core'
            implementationClass = 'org.grails.gradle.plugin.core.GrailsGradlePlugin'
        }
        grailsDoc {
            displayName = "Grails Doc Gradle Plugin"
            description = 'Adds Grails doc publishing support'
            id = 'org.grails.grails-doc'
            implementationClass = 'org.grails.gradle.plugin.doc.GrailsDocGradlePlugin'
        }
        grailsGsp {
            displayName = "Grails GSP Gradle Plugin"
            description = 'A plugin that adds support for compiling Groovy Server Pages (GSP)'
            id = 'org.grails.grails-gsp'
            implementationClass = 'org.grails.gradle.plugin.web.gsp.GroovyPagePlugin'
        }
        grailsPlugin {
            displayName = "Grails-Plugin Gradle Plugin"
            description = 'A Gradle plugin for Grails plugins'
            id = 'org.grails.grails-plugin'
            implementationClass = 'org.grails.gradle.plugin.core.GrailsPluginGradlePlugin'
        }
        grailsProfile {
            displayName = "Grails Profile Gradle Plugin"
            description = 'A plugin that is capable of compiling a Grails profile into a JAR file for distribution'
            id = 'org.grails.grails-profile'
            implementationClass = 'org.grails.gradle.plugin.profiles.GrailsProfileGradlePlugin'
        }
        grailsWeb {
            displayName = "Grails Web Gradle Plugin"
            description = 'Adds web specific extensions'
            id = 'org.grails.grails-web'
            implementationClass = 'org.grails.gradle.plugin.web.GrailsWebGradlePlugin'
        }
        grailsPluginPublish {
            displayName = "Grails Publish Gradle Plugin"
            description = 'A plugin to setup publishing to Grails central repo'
            id = 'org.grails.internal.grails-plugin-publish'
            implementationClass = 'org.grails.gradle.plugin.publishing.internal.GrailsCentralPublishGradlePlugin'
        }
        grailsProfilePublish {
            displayName = "Grails Profile Publish Plugin"
            description = 'A plugin for publishing profiles'
            id = 'org.grails.internal.grails-profile-publish'
            implementationClass = 'org.grails.gradle.plugin.profiles.internal.GrailsProfilePublishGradlePlugin'
        }
    }
}

jar {
    manifest.mainAttributes(
            "Built-By": System.properties['user.name'],
            "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
            "Implementation-Title": "Grails",
            "Implementation-Version": projectVersion,
            "Implementation-Vendor": 'grails.org')
}

configure([compileGroovy, compileTestGroovy]) {
    groovyOptions.fork(memoryInitialSize: '128M', memoryMaximumSize: '1G')
    groovyOptions.encoding = "UTF-8"
    options.encoding = "UTF-8"
}

configure([compileJava, compileTestJava]) {
    options.deprecation = true
    options.debug = true
}

configure([groovydoc]) {
    destinationDir = project.file("build/docs/api")
    classpath += configurations.documentation
}

configure([javadoc]) {
    options.encoding "UTF-8"
    options.docEncoding "UTF-8"
    options.charSet "UTF-8"
    options.jFlags "-Xms64M", "-Xmx512M"
}
