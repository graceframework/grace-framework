dependencies {
    api project(":grace-api")
    api project(":grace-bootstrap")
    api project(":grace-plugin-api")
    api project(":grace-spring")
    api project(":grace-util")

    api libs.gorm.datastore.core
    api libs.spring.beans
    api libs.spring.boot
    api libs.spring.context
    api libs.spring.core
    api libs.spring.tx
    compileOnly libs.micronaut.spring.context

    testImplementation project(":grace-test")
    testImplementation libs.spring.jdbc
    testImplementation libs.spring.test
}

def writeProps = tasks.register('writeGrailsProperties', WriteProperties) {
    outputFile = file("${buildDir}/grails.build.properties")
    property("grails.version", project.version)
}

tasks.named('processResources') {
    from writeProps
}

eclipse {
    classpath {
       file {
           whenMerged { classpath ->
                // don't add global ASTs since the source code is in this project and that causes greclipse compilation to fail
                classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "src/main/resources" }?.excludes=["META-INF/services/org.codehaus.groovy.transform.ASTTransformation"]
           }
       }
    }
}
