configurations.testCompileClasspath {
    exclude module: "grails-plugin-testing"
}

dependencies {
    api project(":grails-api")
    api project(":grails-util")
    api project(":grails-test-suite-base")
    api project(":grails-plugin-interceptors")
    api project(":grails-plugin-controllers")

    api libs.tomcat.jdbc

    testRuntimeOnly libs.javax.jsp
    testRuntimeOnly libs.javax.jstl
    testRuntimeOnly libs.javax.el
    testRuntimeOnly libs.h2
    testRuntimeOnly libs.spring.aspects
    testRuntimeOnly libs.aspectj.rt
    testRuntimeOnly libs.aspectj.weaver

    testImplementation libs.micronaut.spring.context

    testImplementation project(":grails-boot")
    testImplementation project(":grails-plugin-codecs")
    testImplementation project(":grails-plugin-domain-class")
    testImplementation project(":grails-plugin-url-mappings")
    testImplementation project(":grails-plugin-datasource")
    testImplementation project(":grails-plugin-services")
    testImplementation project(":grails-plugin-rest")
    testImplementation project(":grails-plugin-i18n")
    testImplementation project(":grails-plugin-databinding")
    testImplementation project(":grails-spring")
    testImplementation project(":grails-test-support")

    testImplementation libs.gorm.datastore.gorm.hibernate5, {
        exclude group: 'org.grails', module:'grails-datastore-gorm'
        exclude group: 'org.grails', module:'grails-datastore-core'
        exclude group: 'org.springframework', module:'spring-core'
        exclude group: 'org.springframework', module:'spring-context'
        exclude group: 'org.springframework', module:'spring-web'
        exclude group: 'org.springframework', module:'spring-beans'
        exclude group: 'org.springframework', module:'spring-tx'
        exclude group: 'org.slf4j', module:'jcl-over-slf4j'
        exclude group: 'org.slf4j', module:'jul-to-slf4j'
        exclude group: 'org.slf4j', module:'slf4j-api'
        exclude group: 'org.slf4j', module:'slf4j-simple'
        exclude group: 'org.grails', module:'grails-bootstrap'
        exclude group: 'org.grails', module:'grails-plugin-domain-class'
        exclude group: 'org.grails', module:'grails-core'
        exclude group: 'org.grails', module:'grails-web'
        exclude group: 'org.grails', module:'grails-test'
        exclude group: 'org.grails', module:'grails-async'
        exclude group: 'org.grails', module:'grails-spring'
        exclude group: 'commons-collections', module:'commons-collections'
        exclude group: 'commons-lang', module:'commons-lang'
        exclude group: 'javassit', module:'javassist'
        exclude group: 'java-persistence', module:'persistence-api'
        exclude group: 'javassist', module: 'javassist'
    }

    testImplementation libs.grails.async, {
        exclude module:'grails-plugin-codecs'
        exclude module:'grails-plugin-controllers'
        exclude module:'grails-core'
        exclude module:'grails-plugin-domain-class'
    }
    testImplementation libs.grails.plugins.gsp, {
        exclude module:'grails-plugin-codecs'
        exclude module:'grails-plugin-controllers'
        exclude module:'grails-core'
        exclude module:'grails-plugin-domain-class'
        exclude module:'grails-web-common'
        exclude module:'grails-encoder'
    }
}

test {
     maxParallelForks = isCiBuild ? 2 : 4
     forkEvery = isCiBuild ? 25 : 100
     if(!isCiBuild) {
         maxHeapSize = '1024m'
     }
     excludes = [
         "**/grails/test/PersonTests.class",
         "**/rest/render/**/*Spec.class",
         "**/*TestCase.class",
         "**/DataSourceGrailsPluginTests",
         "**/DefaultGrailsControllerClassTests.class",
         "**/GrailsUnitTestCaseTests.class",
         "**/SetupTeardownInvokeTests.class",
         "**/TestMixinSetupTeardownInvokeTests.class",
         "**/UrlMappingsTestMixinTests.class",
         "**/WebUtilsTests.class",
         "**/RestfulControllerSpec.class",
         "**/ResourceAnnotationRestfulControllerSpec.class",
         "**/TestingValidationSpec.class",
         "**/CascadingErrorCountSpec"
     ]
}

task isolatedTestsOne(type:Test) {
    includes = [
        "**/DataSourceGrailsPluginTests.class",
        "**/GrailsUnitTestCaseTests.class",
        "**/WebUtilsTests.class"
    ]
}

task isolatedTestsTwo(type:Test) {
    maxParallelForks = 1
    forkEvery = 100
    includes = [
        "**/UrlMappingsTestMixinTests.class",
        "**/SetupTeardownInvokeTests.class",
        "**/TestMixinSetupTeardownInvokeTests.class"
    ]
}

task isolatedRestRendererTests(type:Test) {
    includes = ['**/rest/render/**/*Spec.class']
}

task isolatedDefaultGrailsControllerClassTests(type: Test) {
    includes = ['**/DefaultGrailsControllerClassTests.class']
}

task isolatedPersonTests(type: Test) {
    includes = ['**/grails/test/PersonTests.class',
                "**/TestingValidationSpec.class",
                "**/CascadingErrorCountSpec"
                ]
}

task isolatedRestfulControllerTests(type:Test) {
    includes = ["**/RestfulControllerSpec.class",
        "**/ResourceAnnotationRestfulControllerSpec.class"]
}

test.dependsOn isolatedPersonTests, isolatedTestsOne, isolatedTestsTwo, isolatedRestRendererTests, isolatedDefaultGrailsControllerClassTests, isolatedRestfulControllerTests
