import com.github.benmanes.gradle.versions.updates.resolutionstrategy.ComponentSelectionRulesWithCurrent
import com.github.benmanes.gradle.versions.updates.resolutionstrategy.ComponentSelectionWithCurrent

buildscript {
    repositories {
        jcenter()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "se.patrikerdes:gradle-use-latest-versions-plugin:0.2.15"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.36.0"
    }
}

repositories {
    jcenter()
    maven { url "https://repo.grails.org/grails/core" }
}
version project.projectVersion

ext {
    commonBuild = 'https://raw.githubusercontent.com/grails/grails-common-build/v2.0.0.M1'
}

apply plugin:'idea'

subprojects {

    apply plugin: 'se.patrikerdes.use-latest-versions'
    apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    if(isGrailsPlugin) {
        group "org.grails.plugins"
    }
    else {
        group "org.grails"
    }
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    if(it.name.startsWith("examples")) {
        apply plugin:"org.grails.grails-web"
    }
    else if(isGrailsPlugin) {
        apply plugin:"org.grails.grails-plugin"
        apply from:"${commonBuild}/common-publishing.gradle"
    }
    else {
        apply from:"${commonBuild}/common-project.gradle"
        apply from:"${commonBuild}/common-publishing.gradle"
        dependencies {
            documentation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
        }
    }

    apply plugin:'idea'
    apply plugin: 'java-library'
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def dependencyName = details.requested.name
            if(dependencyName.startsWith('mongodb-driver') &&  !dependencyName.contains('-rx')) {
                details.useVersion(mongodbDriverVersion)
            }
            if( details.requested.group == 'org.grails' &&
                    details.requested.name in ['grails-datastore-core',
                                               'grails-datastore-async',
                                               'grails-datastore-gorm',
                                               'grails-datastore-gorm-async',
                                               'grails-datastore-gorm-rx',
                                               'grails-datastore-gorm-support',
                                               'grails-datastore-gorm-tck',
                                               'grails-datastore-gorm-test',
                                               'grails-datastore-gorm-validation',
                                               'grails-datastore-web']
            ) {
                details.useVersion(datastoreVersion)
            } else if (details.requested.group == "org.springframework") {
                details.useVersion(springVersion)
            } else if (details.requested.group == "org.springframework.boot") {
                details.useVersion(springBootVersion)
            }
        }
    }

    dependencies {
        compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"

        testImplementation "javax.servlet:javax.servlet-api:$servletApiVersion"
        testImplementation "org.codehaus.groovy:groovy-test-junit5:${groovyVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
        testImplementation "org.junit.platform:junit-platform-runner:1.7.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }

    tasks.named("dependencyUpdates").configure {com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask task->

        // disallow release candidates as upgradable versions from stable versions
        task.rejectVersionIf {
            isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
        }
    }

    useLatestVersions {
        updateRootProperties = true
    }
}

apply from:"${commonBuild}/common-docs.gradle"

boolean isNonStable(String version) {
    version ==~ /(?i).+(-|\.?)(b|M|RC|Dev)\d?.*/ ||
            ['alpha', 'beta', 'milestone', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
}
