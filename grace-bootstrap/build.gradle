import java.time.*
import java.time.format.DateTimeFormatter

plugins {
    id "com.gorylenko.gradle-git-properties" version libs.versions.gradle.git.properties
}

dependencies {
    api project(":grails-util")

    api libs.groovy.templates
    api libs.groovy.xml
    api libs.snakeyaml
    api libs.micronaut.inject
    compileOnly libs.groovy.ant
    compileOnly libs.jansi
    compileOnly libs.jline
    compileOnly libs.jna
    compileOnly libs.methvin.directoryWatcher

    testImplementation libs.jansi
    testImplementation libs.jline
}

gitProperties {
    gitPropertiesName = "grails-build.properties"
    dateFormat = "yyyy-MM-dd HH:mm:ss z"
    dateFormatTimeZone = "UTC"
    customProperty 'grails.buildTime', { getBuildTime() }
    customProperty 'grails.version', { project.version }
}

def getBuildTime() {
    ZonedDateTime.now(ZoneId.of('UTC')).format(DateTimeFormatter.ofPattern('yyyy-MM-dd HH:mm:ss z'))
}
