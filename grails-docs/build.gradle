configurations {
    // Required to keep Gradle classes off the test compile classpath.
    gradleConf.extendsFrom compileClasspath
}

dependencies {
    gradleConf gradleApi()
    api libs.asciidoctorj
    api libs.ant
    api libs.commons.lang
    api libs.grails.gdoc.engine
    api libs.groovy.ant
    api libs.jsoup
    api libs.slf4j.jcl
    api libs.snakeyaml
    api libs.xhtmlrenderer, {
        exclude group: 'bouncycastle', module:'bcprov-jdk14'
    }

    runtimeOnly libs.slf4j.api
    runtimeOnly libs.itext

    testImplementation libs.spock.core, { transitive = false }
}

sourceSets {
    main {
        compileClasspath = configurations.gradleConf
    }
}

tasks.register("docFilesJar", Jar) {
    description = 'Package up files used for generating documentation.'
    archiveVersion = null
    archiveFileName = "grails-doc-files.jar"
    from "src/main/template"
}

tasks.named('jar', Jar) {
    from docFilesJar
}

jar.dependsOn docFilesJar

eclipse {
    classpath {
       plusConfigurations += [configurations.gradleConf]
    }
}
