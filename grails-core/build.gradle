dependencies {
    api project(":grails-api")
    api project(":grails-bootstrap")
    api project(":grails-plugin-api")
    api project(":grails-spring")
    api project(":grails-util")

    api libs.gorm.datastore
    api libs.spring.boot
    api libs.spring.beans
    api libs.spring.context
    api libs.spring.core
    api libs.spring.tx
    compileOnly libs.micronaut.spring.context

    testImplementation project(":grails-test")
    testImplementation libs.spring.jdbc
    testImplementation libs.spring.test
}

def writeProps = tasks.register('writeGrailsProperties', WriteProperties) {
    outputFile = file("${buildDir}/grails.build.properties")
    property("grails.version", project.version)
}

tasks.named('processResources') {
    from writeProps
}

eclipse {
    classpath {
       file {
           whenMerged { classpath ->
                // don't add global ASTs since the source code is in this project and that causes greclipse compilation to fail
                classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "src/main/resources" }?.excludes=["META-INF/services/org.codehaus.groovy.transform.ASTTransformation"]
           }
       }
    }
}
